plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.20.0'
}

group 'com.dpforge'
version '0.0.2'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

gradlePlugin {
    plugins {
        main {
            id = 'com.dpforge.manifestguard'
            implementationClass = 'com.dpforge.manifestguard.ManifestGuardPlugin'
            displayName = "Manifest Guard"
            description = "Plugin for Android applications for detecting unexpected changes in AndroidManifest.xml"
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId = group
            artifactId = 'manifestguard'
            version = version
        }
    }
}

pluginBundle {
    website = 'https://github.com/int02h/manifest-guard'
    vcsUrl = 'https://github.com/int02h/manifest-guard'
    tags = ['android', 'androidmanifest']
}

dependencies {
    implementation 'com.android.tools.build:gradle:7.0.4'
    implementation 'org.apache.commons:commons-text:1.9'

    testImplementation(
            gradleTestKit(),
            "com.android.tools:common:30.0.4"
    )

    testImplementation("org.junit.jupiter:junit-jupiter:5.7.1")
}

test {
    environment "ANDROID_HOME", getSdkDir()
    useJUnitPlatform()
}

private String getSdkDir() {
    def androidHome = System.getenv("ANDROID_HOME")
    if (androidHome != null) {
        return androidHome
    }
    def localProperties = new Properties()
    localProperties.load(rootProject.file("local.properties").newDataInputStream())
    return localProperties["sdk.dir"]
}